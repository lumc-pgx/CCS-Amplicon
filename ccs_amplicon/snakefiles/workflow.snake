SUBWORKFLOW_PROFILE = config.get("PROFILE", "")
if SUBWORKFLOW_PROFILE != "":
    SUBWORKFLOW_PROFILE = "--profile " + SUBWORKFLOW_PROFILE


rule all:
    input:
        "haplotypes/{}.fastq".format(config["PREFIX"]),
        "phasing/{}.html".format(config["PREFIX"])
       

rule filter_ccs_bam:
    input: config["CCS_BAM"]
    output: "filtered/{prefix}.bam",
    message: "filtering CCS bam file"
    shell:
        "bamtools filter -in {input} -length '>={config[MIN_READ_LENGTH]}' | "
        "bamtools filter -length '<={config[MAX_READ_LENGTH]}' | "
        "bamtools filter -tag 'np:>={config[MIN_PASSES]}' | "
        "bamtools filter -out {output} -tag 'rq:>={config[MIN_QUAL]}'"


rule pbindex:
    input: rules.filter_ccs_bam.output
    output: "filtered/{prefix}.bam.pbi"
    message: "generating pacbio index for bam"
    shell: "pbindex {input}"


rule bam2fasta:
    input:
        bam = rules.filter_ccs_bam.output,
        index = rules.pbindex.output
    output: "filtered/{prefix}.fasta"
    message: "converting bam to fasta"
    shell: "bam2fasta -o filtered/{wildcards.prefix} -u {input.bam}"


rule trim_fasta:
    input: rules.bam2fasta.output
    output: "filtered/{prefix}.trimmed.fasta"
    message: "trimming sequence ends"
    shell: "ccs_amplicon.trim_fasta -n {config[TRIM_ENDS]} {input} > {output}"


rule collapse_hp:
    input: rules.trim_fasta.output
    output: "collapsed/{prefix}.collapse_hp.fasta"
    message: "collapsing homopolymer runs"
    shell: "ccs_amplicon.collapse -s {config[MAX_HP_LEN]} {input} > {output}"


rule distance_matrix:
    input: rules.collapse_hp.output
    output: "clustering/{prefix}.dists.tsv"
    message: "creating distance matrix"
    shell: "ccs_amplicon.distance {input} > {output}"


rule embed:
    input: rules.distance_matrix.output
    output:
        embeddings = "clustering/{prefix}.embedded.tsv",
        history = "clustering/{prefix}.embedded.log"
    message: "embedding in two dimensions using tSNE"
    shell: "ccs_amplicon.embed -i {config[TSNE_ITERS]} -lr {config[TSNE_LR]} "
           "{input} > {output.embeddings} 2>{output.history}"


rule ccs_info:
    input: rules.filter_ccs_bam.output
    output: "filtered/{prefix}.bam.info"
    message: "generating summary information for CCS sequences"
    shell: "ccs_amplicon.baminfo {input} > {output}"


rule cluster:
    input:
        embeddings = rules.embed.output.embeddings,
        info = rules.ccs_info.output
    output: "clustering/{prefix}.clusters.json"
    message: "identifying clusters"
    shell: "export OMP_NUM_THREADS=1 && ccs_amplicon.clusters -s {config[CLUSTER_SIMILARITY_PERCENTILE]} "
           "-i {config[CLUSTER_INFLATION]} "
           "{input.embeddings} {input.info} > {output}"

rule filter_clusters:
    input:
        clusters = rules.cluster.output,
        info = rules.ccs_info.output
    output: "clustering/{prefix}.clusters.filtered.json"
    message: "filtering clusters"
    shell:
        "ccs_amplicon.filter_clusters -i {config[CLUSTER_SIZE_THRESHOLD]} -m {config[MAX_CLUSTER_SIZE]} "
        "{input.clusters} {input.info} > {output}"


rule cluster_seqs:
    input:
        seqs = rules.collapse_hp.output,
        clusters = rules.filter_clusters.output
    output: dynamic("clustering/{prefix}.cluster{clusterid}.fasta")
    message: "creating per-cluster sequence files"
    shell: "ccs_amplicon.seqs_from_clusters -p clustering/{wildcards.prefix} {input.seqs} {input.clusters}"


rule make_haplotypes:
    input: rules.cluster_seqs.output
    output:
        alleles = "merge_laa/{prefix}.cluster{clusterid}.fastq",
        tagged_bams = "phasing/{prefix}.cluster{clusterid}.tagged.bam"
    message: "running haplotype workflow for cluster {wildcards.clusterid}"
    params: snakefile = srcdir("subworkflow.snake")
    shell: "snakemake -s {params.snakefile} --rerun-incomplete --nolock "
           "{SUBWORKFLOW_PROFILE} "
           "--config PREFIX={wildcards.prefix}.cluster{wildcards.clusterid} "
           "CCS_BAM={config[CCS_BAM]} "
           "SUBREADS_BAM={config[SUBREADS_BAM]} "
           "CONSENSUS_FRACTION={config[CONSENSUS_FRACTION]} "
           "MIN_HAPLOTYPE_MOLECULES={config[MIN_HAPLOTYPE_MOLECULES]} "
           "MIN_VARIANT_QUAL={config[MIN_VARIANT_QUAL]}"


rule merge_haplotype_clusters:
    input: dynamic("merge_laa/{prefix}.cluster{clusterid}.fastq")
    output: "haplotypes/{prefix}.fastq"
    message: "merging haplotype sequences"
    shell: "cat {input} > {output}"


rule summarize_phasing:
    input:
        tagged_bams = dynamic("phasing/{prefix}.cluster{clusterid}.tagged.bam"),
        info = rules.ccs_info.output,
        clusters = rules.cluster.output
    output: "phasing/{prefix}.phasing.tsv"
    message: "summarizing phasing"
    shell: "ccs_amplicon.summarize_phasing {input.info} {input.clusters} {input.tagged_bams} > {output}"


rule view_clustering:
    input:
        embeddings = rules.embed.output.embeddings,
        info = rules.summarize_phasing.output
    output:
        "phasing/{prefix}.html"
    message:
        "visualizing phased clusters"
    shell: "ccs_amplicon.clusterplot -t {wildcards.prefix} "
           "{input.embeddings} {input.info} {output}"

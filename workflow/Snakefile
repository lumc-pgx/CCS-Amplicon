CCS_BAM = "/exports/humgen/wgallard/pharmacogenomics_pipeline/pugwash/LIMA_2_stage_deflate/plate2/preprocessor/CCS/lbc20.bam"
MIN_READ_LENGTH = 6000
MAX_READ_LENGTH = 7000
MIN_PASSES = 1
MIN_QUAL = 0.98
MAX_HP_LEN = 3
TSNE_ITERS = 5000
TSNE_LR = 50
CLUSTER_SIMILARITY_PERCENTILE = 80
CLUSTER_INFLATION = 1.4
CLUSTER_COVERAGE_LIMIT = 500
CONSENSUS_FRACTION = 0.51

SMRTCMD_PATH = "/usr/local/smrtlink/smrtcmds/bin"
SCRIPT_DIR = srcdir("scripts")


rule all:
    input: dynamic("consensus/ccs.filtered.cluster{clusterid}.consensus.fasta")


rule filter_ccs_bam:
    input: CCS_BAM
    output: "filtered/ccs.filtered.bam"
    message: "filtering CCS bam file"
    shell:
        "bamtools filter -in {input} -out {output} -length '>={MIN_READ_LENGTH}' -length '<={MAX_READ_LENGTH}' "
        "-tag 'np:>={MIN_PASSES}' -tag 'rq:>={MIN_QUAL}'"


rule pb_index:
    input: "{prefix}.bam"
    output: "{prefix}.bam.pbi"
    message: "generating index for filtered CCS bam file"
    shell: "{SMRTCMD_PATH}/pbindex {input}"


rule bam2fasta:
    input:
        bam = "filtered/{prefix}.bam",
        index = "filtered/{prefix}.bam.pbi"
    output: "filtered/{prefix}.fasta"
    message: "converting bam to fasta"
    shell: "{SMRTCMD_PATH}/bam2fasta -o - {input.bam} > {output}"


rule collapse_hp:
    input: "filtered/{prefix}.fasta"
    output: "collapsed/{prefix}.collapse_hp.fasta"
    message: "collapsing homopolymer runs"
    shell: "{SCRIPT_DIR}/collapse_homopolymers.py -s {MAX_HP_LEN} {input} > {output}"


rule distance_matrix:
    input: "collapsed/{prefix}.collapse_hp.fasta"
    output: "clustering/{prefix}.dists.tsv"
    message: "creating distance matrix"
    shell: "{SCRIPT_DIR}/distance_matrix.py {input} > {output}"


rule embed:
    input: "clustering/{prefix}.dists.tsv"
    output:
        embeddings = "clustering/{prefix}.embedded.tsv",
        history = "clustering/{prefix}.embedded.log"
    message: "embedding in two dimensions using tSNE"
    shell: "{SCRIPT_DIR}/embed.py -i {TSNE_ITERS} -lr {TSNE_LR} {input} > {output.embeddings} 2>{output.history}"


rule ccs_info:
    input: "filtered/{prefix}.bam"
    output: "filtered/{prefix}.bam.info"
    message: "generating summary information for CCS sequences"
    shell: "{SCRIPT_DIR}/baminfo.py {input} > {output}"


rule cluster:
    input:
        embeddings = "clustering/{prefix}.embedded.tsv",
        info = "filtered/{prefix}.bam.info"
    output: "clustering/{prefix}.clusters.json"
    message: "identifying clusters"
    shell: "{SCRIPT_DIR}/cluster.py -s {CLUSTER_SIMILARITY_PERCENTILE} -i {CLUSTER_INFLATION} "
           "-c {CLUSTER_COVERAGE_LIMIT} {input.embeddings} {input.info} > {output}"


rule cluster_seqs:
    input:
        seqs = "collapsed/{prefix}.collapse_hp.fasta",
        clusters = "clustering/{prefix}.clusters.json"
    output: dynamic("clustering/{prefix}.cluster{clusterid}.fasta")
    message: "creating per-cluster sequence files"
    shell: "{SCRIPT_DIR}/seqs_from_clusters.py -p clustering/{wildcards.prefix} {input.seqs} {input.clusters}"


rule mafft_msa:
    input: "clustering/{prefix}.cluster{clusterid}.fasta"
    output: "consensus/{prefix}.cluster{clusterid}.msa"
    message: "mafft alignment of cluster sequences"
    shell: "mafft --auto --quiet {input} > {output}"


rule consensus:
    input: "consensus/{prefix}.cluster{clusterid}.msa"
    output: "consensus/{prefix}.cluster{clusterid}.consensus.fasta"
    message: "generating cluster consensus sequence"
    shell: "{SCRIPT_DIR}/consensus.py -f {CONSENSUS_FRACTION} "
           "-s {wildcards.prefix}.cluster{wildcards.clusterid}.consensus {input} > {output}"